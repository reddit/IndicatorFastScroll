def libraryArtifactId = 'indicator-fast-scroll'
def libraryVersionName = '1.3.0'

group = 'com.reddit'
version = libraryVersionName

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName libraryVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions.unitTests.includeAndroidResources = true

    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    libraryVariants.all {
        it.generateBuildConfigProvider.configure { task ->
            task.enabled = false
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Creates a Maven publication called "release".
                release(MavenPublication) {
                    from components.release
                    artifactId = libraryArtifactId
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_KEY')
    publications = ['release']
    pkg {
        repo = 'IndicatorFastScroll'
        name = libraryArtifactId
        userOrg = 'reddit'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/reddit/IndicatorFastScroll.git'
        version {
            name = libraryVersionName
        }
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    api 'androidx.recyclerview:recyclerview:1.0.0'
    api 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'androidx.core:core-ktx:1.0.2'

    testImplementation "androidx.test.ext:junit:1.1.1"
    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'org.robolectric:robolectric:4.5.1'
}
